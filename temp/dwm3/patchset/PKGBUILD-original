pkgname=dwm-git
_gitname=dwm
pkgver=6.0.32.g7edc596
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama')
makedepends=('git')
install=dwm.install
provides=('dwm')
conflicts=('dwm')
source=('git+http://git.suckless.org/dwm'
        'config.h'
        'monocle-no-border.patch'
        dwm.desktop)
md5sums=('SKIP'
         '4f2e17e19feb92b89ba0f8893b811e25'
         'ea218fd4823ea9b5cbf6b129473b3a2a'
         '939f403a71b6e85261d09fc3412269ee')

pkgver() {
  cd $_gitname
  git describe --always --tags | sed 's|-|.|g'
}

prepare() {
  cd $_gitname
  cp $srcdir/config.h config.h
  patch -p1 < $srcdir/monocle-no-border.patch
  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS   = -std/CFLAGS   += -std/g' config.mk
}

build() {
  cd $_gitname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_gitname
  make PREFIX=/usr DESTDIR=$pkgdir install
  install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
  install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
  install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
}

# vim:set ts=2 sw=2 et:
